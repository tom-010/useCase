grammar io.deniffel.dsl.useCase.UseCase with org.eclipse.xtext.common.Terminals

generate useCase "http://www.deniffel.io/dsl/useCase/UseCase"

Model:
	(types+=Type)*
	(useCases+=UseCase)*
;
 
UseCase:
	'use-case' name=STRING
	(descriptions+=Description)?
	(allowedUserGroups+=AllowedUserGroups)?
	(inputs+=Inputs)?
	(outputs+=Outputs)?
	(steps+=Steps)?
	(notes+=Notes)?
	'end' 'of' 'use-case';

Steps: 
	'steps:'
		(steps+=Step)*;

Step:
	number=QualifiedNumber action=STRING;

Description:
	'description:' name=STRING;

Notes:
	('notes:'|'note:') content=STRING;
	
Inputs:
	('requires:'|'require:'|'requires' 'the' 'following' 'input' ':')
	(inputs+=Input)*;
	
Outputs:
	('produces:'|'produce:'|'produces' 'the' 'following' 'output' ':')
	(outputs+=Output)*;

Type:
    'type' name=ID '('description=STRING')' ('['importInfo=QualifiedName']' )?;
    
Input:
	(many?='many')? '-' content=STRING ('as' type=[Type])?;
	
Output:
	(many?='many')? '-' content=STRING ('as' type=[Type])?;
	
AllowedUserGroups:
	'allowed-usergroups:'
	(groups+=AllowedUserGroup )*;
  
AllowedUserGroup:
	(many?='many')? '-' name=STRING ('as' type=[Type])?;
  
QualifiedName:
    ID ('.' ID)*;
    
QualifiedNumber:
	INT ('.' INT)*'.';
